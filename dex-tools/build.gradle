plugins {
    id "application"
    id "dex2jar.build-conventions"
}

description = 'Dex/Dalvik tools for dex2jar'

dependencies {
    api project(':dex-translator')
    api project(':d2j-smali')
    api project(':d2j-jasmin')
    api project(':dex-writer')
    api project(':d2j-base-cmd')
    api project(path: ':d2j-external', configuration: 'shadow')
    // api libs.asm // Fixed MethodCodeTooLargeException
    api(libs.asm.tree) {
        exclude(module: 'asm')
    }
    api(libs.asm.util) {
        exclude(module: 'asm')
    }
    api(libs.asm.commons) {
        exclude(module: 'asm')
    }
    testImplementation libs.antlr3.runtime
}

tasks.register('bin_gen', JavaExec) {
    dependsOn jar, ':d2j-external:shadowJar'
    classpath sourceSets.main.runtimeClasspath
    ext.binDir = "${layout.buildDirectory.asFile.get()}/generated-sources/bin"
    outputs.dir file(ext.binDir)
    mainClass.set('com.googlecode.dex2jar.bin_gen.BinGen')
    args = ["$projectDir/src/main/bin_gen", "$ext.binDir"]
}

application {
    applicationDistribution.from(bin_gen)
    applicationDistribution.from('open-source-license.txt') {
        into("lib")
    }
    applicationDistribution.from("$parent.rootDir") {
        include("NOTICE.txt")
        include("LICENSE.txt")
    }
}

startScripts.dependsOn bin_gen
